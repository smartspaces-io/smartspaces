/*
 * Copyright (C) 2016 Keith M. Hughes
 * Copyright (C) 2013 Google Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not
 * use this file except in compliance with the License. You may obtain a copy of
 * the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
 * WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the * License for the specific language governing permissions and limitations under
 * the License.
 */

ext {
  serviceFolderName = "smartspaces.service.image.depth"
  baseServiceBuildFolder = "${project.ext.extrasBuildFolder}/${project.ext.serviceFolderName}"
}

dependencies {
  compile project(':smartspaces-core')
  compile project(':activityapi:smartspaces-interaction')
  compile project(':service:smartspaces-service-image-depth-openni2')

  compile('com.nativelibs4java:bridj:0.7.0') { exclude module: 'dx'  }

  provided "${project.ext.osgiContainerDependency}"

  testCompile 'junit:junit:4.8.2'
  testCompile 'org.mockito:mockito-all:1.9.5'
}

task copyResources(type: Copy) {
  from 'src/main/resources/extras'
  into "${project.ext.baseServiceBuildFolder}"
}

task copyExtrasNativeLibs1(type: Copy) {
  from "${project.getProperty('smartspaces.dependencies.home')}/openni/2.2/native"
  into "${project.ext.baseServiceBuildFolder}/openni/native"
  exclude '**/*.a'
}

task copyExtrasNativeLibs2(type: Copy) {
  from "${project.getProperty('smartspaces.dependencies.home')}/nite/2.2/native"
  into "${project.ext.baseServiceBuildFolder}/nite/native"
  exclude '**/*.a'
}

task copyExtraComponents(dependsOn: [
  copyResources,
  copyExtrasNativeLibs1,
  copyExtrasNativeLibs2
])

task copyServiceArtifacts(type: Copy, dependsOn: [install]) {
  from configurations.archives.allArtifacts.files
  into "${project.ext.baseServiceBuildFolder}"
}

task assembleExtras(dependsOn: [copyExtraComponents, copyServiceArtifacts])

jar {
  bnd """
    Bundle-SymbolicName: smartspaces.service.image.depth
    Bundle-Version: ${osgiVersion}

    Bundle-Activator: \\
        io.smartspaces.service.image.depth.internal.osgi.DepthCameraServiceActivator

    Export-Package: \\
        !io.smartspaces.service.image.depth.internal.*, \\
        io.smartspaces.service.image.depth.*'

    Private-Package: \\
        io.smartspaces.service.image.depth.internal.*

    Import-Package: \\
        org.bridj.*;version="0.0.0", \\
        io.smartspaces.service.image.depth.internal.openni2.libraries;version="0.0.0", \\
        *
  """
}
